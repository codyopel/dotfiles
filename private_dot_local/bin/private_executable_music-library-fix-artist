#!/usr/bin/env elvish
# vim: ft=elvish


use github.com/chlorm/elvish-stl/path
use github.com/chlorm/elvish-stl/str


var ROOT = $pwd

fn fix-artist {|f|
    echo 't: '$f >&2
    var artists = [ ]
    try {
        var artist = (e:kid3-cli -c 'get ARTIST' $f)
        set artists = [ (str:split ';' $artist) ]
        if (< (count $artists) 2) {
            continue
        }
        e:kid3-cli (for i [ (range 0 (count $artists)) ] { put '-c' 'set ARTIST['$i'] "'$artists[$i]'"' }) $f
    } catch _ { }
}

fn get-dirs {|path|
    put (path:scandir $path)['dirs']
}

fn get-files {|path|
    put (path:scandir $path)['files']
}

for prefix (get-dirs $ROOT) {
    var prefixDir = (path:join $ROOT $prefix)
    for artist (get-dirs $prefixDir) {
        var artistDir = (path:join $prefixDir $artist)
        for album (get-dirs $artistDir) {
            var albumDir = (path:join $artistDir $album)
            for track (get-files $albumDir) {
                # TODO: Support more extensions
                if (not (==s (path:ext $track) '.ogg')) {
                    continue
                }
                var t = (path:join $albumDir $track)
                put $t
            }
        }
    }
} | peach &num-workers=10 {|x|
    fix-artist $x
}
