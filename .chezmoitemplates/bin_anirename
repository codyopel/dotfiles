#!/usr/bin/env elvish
# vim: ft=elvish:

use github.com/chlorm/elvish-stl/os
use github.com/chlorm/elvish-stl/re


fn rename {|regex repl file &c=$false|
    var newfile = (re:replace $regex $repl $file)
    if (==s $file $newfile) {
        return
    }
    echo $file' -> '$newfile >&2
    if $c {
        os:move $file $newfile
    }
}

fn renameall {|regex repl &c=$false|
    for i [ (put *) ] {
        rename &c=$c $regex $repl $i
    }
}

fn aniname {|@args|
    var c = $false
    if (==s $args[0] 'c') {
        set c = $true
    }

    var group = '\[([a-zA-Z0-9_ -]+)\]'
    var sep = '(?: |\.)'
    var sepl = '(?: - | |\.-\.|\.|_-_|_)'
    var title = '([a-zA-Z0-9!\.,_\(\) -]+?)'
    var ep = '(S[0-9]{2}E[0-9]{2}|(?:E|)[0-9]{2})'
    var eptitle = '(?:'$sepl'([a-zA-Z0-9!,''\. -]+)|)'
    var ext = '\.(mkv|mp4)'
    renameall &c=$c ^
        '^'$group$sep$title$sepl$ep$eptitle$sep'.*'$ext'$' ^
        '$2.$3.$4.mediainfo-$1.$5'
}

aniname $@args
